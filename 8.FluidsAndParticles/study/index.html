<script type="x-shader-fragment" id="addForce" >
    precision highp float;

    uniform vec2 force;
    uniform vec2 center;
    uniform vec2 scale;
    uniform vec2 px;

    varying vec2 uv;

    void main(){
        float distance_ = 1.0 - min(length( (uv - center) / scale), 1.0);
        gl_FragColor    = vec4(force * distance_, 0, 1);
    }
</script>
<script type="x-shader-fragment" id="advect" >
    precision   highp       float;

    uniform     sampler2D   source;
    uniform     sampler2D   velocity;
    uniform     float       dt;
    uniform     float       scale;
    uniform     vec2        px1;

    varying     vec2        uv;

    void main(){
        gl_FragColor = texture2D(source, uv - texture2D(velocity, uv).xy * dt * px1) * scale;
    }
</script>
<script type="x-shader-fragment" id="copy" >
    precision   highp       float;

    uniform     sampler2D   source;

    varying     vec2        uv;

    void main(){
        gl_FragColor = texture2D(source, uv);
    }
</script>
<script type="x-shader-fragment" id="divergence" >
    precision   highp       float;

    uniform     sampler2D   velocity;
    uniform     float       dt;
    uniform     vec2        px;

    varying     vec2        uv;

    void main(){
        float x0 = texture2D(velocity, uv - vec2(px.x, 0)).x;
        float x1 = texture2D(velocity, uv + vec2(px.x, 0)).x;
        float y0 = texture2D(velocity, uv - vec2(0, px.y)).y;
        float y1 = texture2D(velocity, uv + vec2(0, px.y)).y;
        float divergence    = (x1-x0 + y1-y0) * 0.5;
        gl_FragColor        = vec4(divergence); // 4byte representation of a scaler OR 3x redundancy??
    }
</script>
<script type="x-shader-fragment" id="jacobi" >
    precision   highp   float;

    uniform     sampler2D   pressure;
    uniform     sampler2D   divergence;
    uniform     float       alpha;
    uniform     float       beta;
    uniform     vec2        px;

    varying     vec2        uv;

    void main(){
        float x0 = texture2D(pressure, uv - vec2(px.x, 0)).r;
        float x1 = texture2D(pressure, uv + vec2(px.x, 0)).r;
        float y0 = texture2D(pressure, uv - vec2(0, px.y)).r;
        float y1 = texture2D(pressure, uv + vec2(0, px.y)).r;
        float d  = texture2D(divergence, uv).r;
        float relaxed   = (x0 + x1 + y0 + y1 + alpha * d) * beta;
        gl_FragColor    = vec4(relaxed);
    }
</script>
<script type="x-shader-fragment" id="subtractPressureGradient" >
    precision   highp       float;

    uniform     sampler2D   pressure;
    uniform     sampler2D   velocity;
    uniform     float       alpha;
    uniform     float       beta;
    uniform     float       scale;
    uniform     vec2        px;

    varying     vec2        uv;

    void main(){
        float x0    = texture2D(pressure, uv - vec2(px.x, 0)).r;
        float x1    = texture2D(pressure, uv + vec2(px.x, 0)).r;
        float y0    = texture2D(pressure, uv - vec2(0, px.y)).r;
        float y1    = texture2D(pressure, uv + vec2(0, px.y)).r;
        vec2 v      = texture2D(velocity, uv).xy;
        gl_FragColor = vec4( (v - (vec2(x1, y1) - vec2(x0, y0)) * 0.5) * scale, 1.0, 1.0);
    }
</script>
<script type="x-shader-fragment" id="velocityBoundary" >
    precision   highp       float;

    uniform     sampler2D   velocity;
    uniform     float       dt;
    uniform     float       scale;

    varying     vec2        uv;

    vec2 velocityAt(uv) {
        return texture2D(velocity, uv - texture2D(velocity, uv).xy * dt);
    }

    void main(){
        gl_FragColor = vec4(scale * velocityAt(uv), 1.0, 1.0);
    }
</script>
<script type="x-shader-fragment" id="visualize" >
    precision   highp       float;

    uniform     sampler2D   velocity;
    uniform     sampler2D   pressure;

    varying     vec2        uv;

    void main(){
        gl_FragColor = vec4(
            (texture2D(pressure, uv)).x, // Notice we used .r last time to access the 1st byte of pressure
            (texture2D(velocity, uv) * 1.5 + 0.5).xy,
            1.0
        );
    }
</script>

<script type="x-shader-vertex" id="boundary">
    precision   highp   float;

    attribute   vec3    position;
    attribute   vec3    offset;

    varying     vec2    uv;

    void main(){
        uv          = offset.xy * 0.5 + 0.5;
        gl_Position = vec4(position, 1.0);
    }
</script>
<script type="x-shader-vertex" id="cursor">
    precision   highp   float;

    attribute   vec3    position;

    uniform     vec2    center;
    uniform     vec2    px;

    varying     vec2    uv;

    void main(){
        uv          = clamp(position.xy + center, vec2(-1.0 + px * 2.0), vec2(1.0 - px * 2.0));
        gl_Position = vec4(uv, 0.0, 1.0);
    }
</script>
<script type="x-shader-vertex" id="kernel">
    precision   highp   float;

    attribute   vec3    position;

    uniform     vec2    px;
    varying     vec2    uv;

    void main(){
        uv          = vec2(0.5) + (position.xy) * 0.5;
        gl_Position = vec4(position, 1.0);
    }
</script>

<body>
    <canvas id="c" width=1280 height=720></canvas>
 </body>
<script src="src/main.js"></script>
