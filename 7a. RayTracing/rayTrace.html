<html><head><style>body {background-color: black;}</style></head><body></body>
<script id="x-fragment" type="x-shader">
    precision mediump float;
    vec3    lightDir    = 1.07 * normalize(vec3(1.0, 1.0, -1.0));
    vec3    blandColour = 1.05 * normalize(vec3(1.0, 1.0, 1.0));
    float   ambience    = 0.3;
    float   diffussion  = 0.3;
    float   shinyness   = 0.5;
    varying     vec3    vPosition;
    varying     vec3    cameraPostion;
    varying     vec4    spherePos0;
    varying     vec4    spherePos1;
    varying     vec4    spherePos2;
    varying     vec4    spherePos3;
    varying     vec4    spherePos4;
    varying     vec4    spherePos5;
    varying     vec4    spherePos6;
    varying     vec4    spherePos7;
    varying     vec4    spherePos8;
    varying     vec4    spherePos9;
    varying     vec3    sphereCol0;
    varying     vec3    sphereCol1;
    varying     vec3    sphereCol2;
    varying     vec3    sphereCol3;
    varying     vec3    sphereCol4;
    varying     vec3    sphereCol5;
    varying     vec3    sphereCol6;
    varying     vec3    sphereCol7;
    varying     vec3    sphereCol8;
    varying     vec3    sphereCol9;

    bool intersectSphere(vec4 sphere, vec3 rayStart, vec3 rayDirection, out float distance) {
        vec3    c = sphere.xyz - rayStart;
        float   b = dot(rayDirection, c);
        float   d = sphere.w + b * b - dot(c, c);
        if (d < 0.0) {
            distance = 999.0;
            return false;
        }
        distance = b - sqrt(d);
        if (distance < 0.0) {
            distance = 999.0;
            return false;
        }
        return true;
    }

    vec3 lightAt(vec3 instersectNormal, vec3 rayDirection, vec3 color) {
        float visibleLightReflection = dot(reflect(lightDir, instersectNormal), rayDirection);
        if (visibleLightReflection < 0.0) visibleLightReflection = 0.0;
        float saturation = ambience
                + shinyness   * pow(visibleLightReflection, 50.0)
                + diffussion  * dot(lightDir, instersectNormal);
        if (saturation > 1.0){
            float overSaturation = saturation - 1.0;
            return mix(color, 2.0 * blandColour, overSaturation);
        }
        return saturation * color;
    }

    bool intersectWorld(vec3 lStart, inout vec3 rayDirection, out vec3 intersectPosition, inout vec3 endColour, inout vec3 colMultiplier) {
        float d0,d1,d2,d3,d4,d5,d6,d7,d8,d9,distance;
        vec3 instersectNormal, collisionColor, ballCenter;
        bool ground = false;
        bool h0 = intersectSphere(spherePos0, lStart, rayDirection, d0);
        bool h1 = intersectSphere(spherePos1, lStart, rayDirection, d1);
        bool h2 = intersectSphere(spherePos2, lStart, rayDirection, d2);
        bool h3 = intersectSphere(spherePos3, lStart, rayDirection, d3);
        bool h4 = intersectSphere(spherePos4, lStart, rayDirection, d4);
        bool h5 = intersectSphere(spherePos5, lStart, rayDirection, d5);
        bool h6 = intersectSphere(spherePos6, lStart, rayDirection, d6);
        bool h7 = intersectSphere(spherePos7, lStart, rayDirection, d7);
        bool h8 = intersectSphere(spherePos8, lStart, rayDirection, d8);
        bool h9 = intersectSphere(spherePos9, lStart, rayDirection, d9);

             if (h0 && d0 < d9 && d0 < d8 && d0 < d7 && d0 < d6 && d0 < d5 && d0 < d4 && d0 < d3 && d0 < d2 && d0 < d1) {distance = d0; collisionColor = sphereCol0; ballCenter = spherePos0.xyz;}
        else if (h9 && d9 < d8 && d9 < d7 && d9 < d6 && d9 < d5 && d9 < d4 && d9 < d3 && d9 < d2 && d9 < d1)            {distance = d9; collisionColor = sphereCol9; ballCenter = spherePos9.xyz;}
        else if (h8 && d8 < d7 && d8 < d6 && d8 < d5 && d8 < d4 && d8 < d3 && d8 < d2 && d8 < d1)                       {distance = d8; collisionColor = sphereCol8; ballCenter = spherePos8.xyz;}
        else if (h7 && d7 < d6 && d7 < d5 && d7 < d4 && d7 < d3 && d7 < d2 && d7 < d1)                                  {distance = d7; collisionColor = sphereCol7; ballCenter = spherePos7.xyz;}
        else if (h6 && d6 < d5 && d6 < d4 && d6 < d3 && d6 < d2 && d6 < d1)                                             {distance = d6; collisionColor = sphereCol6; ballCenter = spherePos6.xyz;}
        else if (h5 && d5 < d4 && d5 < d3 && d5 < d2 && d5 < d1)                                                        {distance = d5; collisionColor = sphereCol5; ballCenter = spherePos5.xyz;}
        else if (h4 && d4 < d3 && d4 < d2 && d4 < d1)                                                                   {distance = d4; collisionColor = sphereCol4; ballCenter = spherePos4.xyz;}
        else if (h3 && d3 < d2 && d3 < d1)                                                                              {distance = d3; collisionColor = sphereCol3; ballCenter = spherePos3.xyz;}
        else if (h2 && d2 < d1)                                                                                         {distance = d2; collisionColor = sphereCol2; ballCenter = spherePos2.xyz;}
        else if (h1)                                                                                                    {distance = d1; collisionColor = sphereCol1; ballCenter = spherePos1.xyz;}
        else if (rayDirection.y < 0.0)                                                                                  {distance = -lStart.y / rayDirection.y; ground = true;}
        // Our eye and objects are above the floor. Hence can't possibly intersect if ray going upwards
        else return false;

        intersectPosition   = lStart + distance * rayDirection;
        instersectNormal    = intersectPosition - ballCenter;
        if (ground){
            instersectNormal = vec3(0.0, 1.0, 0.0);
            if (length(intersectPosition.xz) > 10.0) return false;

            if (fract(intersectPosition.x) > 0.5 == fract(intersectPosition.z) > 0.5)   collisionColor = vec3(1.0, 1.0, 1.0);
            else                                                                        collisionColor = vec3(0.5, 0.5, 0.0);

        }

        endColour       += lightAt(instersectNormal, rayDirection, collisionColor);
        endColour       *= colMultiplier;
        colMultiplier   *= mix(blandColour, collisionColor, 0.7);
        rayDirection = reflect(rayDirection, instersectNormal);
        return true;
    }

    void main(void) {
        vec3 cameraDir = normalize(vPosition - cameraPostion);
        vec3 intersectPosition1, intersectPosition2, intersectPosition3, dummy;
        vec3 endColour, colMultiplier;
        endColour       = vec3(0.0, 0.0, 0.0);
        colMultiplier   = vec3(1.0, 1.0, 1.0);
        if (intersectWorld(cameraPostion, cameraDir, intersectPosition1, endColour, colMultiplier)) {
            if (intersectWorld(intersectPosition1, cameraDir, intersectPosition2, endColour, colMultiplier)) {
                if (intersectWorld(intersectPosition2, cameraDir, intersectPosition3, endColour, colMultiplier)) {
                        intersectWorld(intersectPosition3, cameraDir, dummy, endColour, colMultiplier);
                }
            }
        } else endColour = vec3(0.0, 0.0, 0.0);
        gl_FragColor = vec4(endColour, 1.0);
    }
</script>
<script id="x-vertex" type="x-shader">
    attribute   float   a_label;
    uniform     float   u_time;
    uniform     float   u_ratio;
    varying     vec3    vPosition;
    varying     vec3    cameraPostion;
    varying     vec4    spherePos0;
    varying     vec4    spherePos1;
    varying     vec4    spherePos2;
    varying     vec4    spherePos3;
    varying     vec4    spherePos4;
    varying     vec4    spherePos5;
    varying     vec4    spherePos6;
    varying     vec4    spherePos7;
    varying     vec4    spherePos8;
    varying     vec4    spherePos9;
    varying     vec3    sphereCol0;
    varying     vec3    sphereCol1;
    varying     vec3    sphereCol2;
    varying     vec3    sphereCol3;
    varying     vec3    sphereCol4;
    varying     vec3    sphereCol5;
    varying     vec3    sphereCol6;
    varying     vec3    sphereCol7;
    varying     vec3    sphereCol8;
    varying     vec3    sphereCol9;
    #define     PI      3.14159265
    void main(void) {
        vec3 cameraTo   = vec3(0.0, 0.0, 0.0);
        vec3 up         = vec3(0.0, 1.0, 0.0);
        float cameraPersp = 5.0;

        sphereCol1    = vec3(0.4, 0.4, 0.4);
        sphereCol4    = vec3(0.8, 0.8, 0.8);
        sphereCol2    = vec3(0.0, 0.5, 0.5);
        sphereCol5    = vec3(0.9, 0.9, 0.5);
        sphereCol6    = vec3(0.2, 0.3, 0.5);
        sphereCol3    = vec3(0.1, 0.1, 0.1);
        sphereCol7    = vec3(0.6, 0.2, 0.2);
        sphereCol8    = vec3(0.9, 0.6, 0.7);
        sphereCol9    = vec3(0.3, 0.6, 0.3);
        sphereCol0    = vec3(0.4, 0.4, 0.7);
        cameraPostion = vec3(+20.0 * sin(u_time * 0.003),              10.0 + 10.0 * sin(u_time * 0.002),      20.0 * cos(u_time * 0.001));
        spherePos1    = vec4( +0.0,                                   +1.0 + 0.5 * abs(sin(u_time * 0.2)),    +0.0,                                   1.0);
        spherePos2    = vec4( +2.0 * cos(u_time * 0.015 + PI),        +1.0,                                   +2.0 * sin(u_time * 0.015 + PI),        1.0);
        spherePos4    = vec4( +0.0,                                   +3.0 + 2.0 * abs(sin(u_time * 0.1)),    +0.0,                                   1.0);
        spherePos5    = vec4( +2.0 * cos(u_time * 0.015 + PI*0.5),    +1.0,                                   +2.0 * sin(u_time * 0.015 + PI*0.5),    1.0);
        spherePos6    = vec4( +2.0 * cos(u_time * 0.015 + PI*1.5),    +1.0,                                   +2.0 * sin(u_time * 0.015 + PI*1.5),    1.0);
        float sep = 1.41;
        spherePos3    = vec4( +3.41 * cos(u_time * 0.015),             +1.0 + sep,                            +3.41 * sin(u_time * 0.015),            1.0);
        spherePos7    = vec4( sep * sin(u_time * 0.015),              -sep,                                    sep * cos(u_time * 0.015),              0.0) + spherePos3;
        spherePos8    = vec4( sep * sin(u_time * 0.015 + PI),         -sep,                                    sep * cos(u_time * 0.015 + PI),         0.0) + spherePos3;
        spherePos9    = vec4( sep * sin(u_time * 0.015 + PI*0.5),     -sep,                                    sep * cos(u_time * 0.015 + PI*0.5),     0.0) + spherePos3;
        spherePos0    = vec4( sep * sin(u_time * 0.015 + PI*1.5),     -sep,                                    sep * cos(u_time * 0.015 + PI*1.5),     0.0) + spherePos3;

        vec3 cameraDir  = normalize(cameraTo - cameraPostion);
        vec3 cameraLeft = normalize(cross(cameraDir, up));
        vec3 cameraUp   = normalize(cross(cameraLeft, cameraDir));
        vec3 cameraCenter= cameraPostion + cameraDir * cameraPersp;
        vec3 cameraTopLeft  = cameraCenter + cameraUp + cameraLeft * u_ratio;
        vec3 cameraBotLeft  = cameraCenter - cameraUp + cameraLeft * u_ratio;
        vec3 cameraTopRight = cameraCenter + cameraUp - cameraLeft * u_ratio;
        vec3 cameraBotRight = cameraCenter - cameraUp - cameraLeft * u_ratio;

        if      (a_label == 1.0) {gl_Position = vec4(+1.0, +1.0, 0.0, 1.0); vPosition = cameraTopRight;}
        else if (a_label == 2.0) {gl_Position = vec4(-1.0, +1.0, 0.0, 1.0); vPosition = cameraTopLeft;}
        else if (a_label == 3.0) {gl_Position = vec4(+1.0, -1.0, 0.0, 1.0); vPosition = cameraBotRight;}
        else if (a_label == 4.0) {gl_Position = vec4(-1.0, -1.0, 0.0, 1.0); vPosition = cameraBotLeft;}
    }
</script>
<script>
    var canvas  = document.createElement("canvas"); document.body.appendChild(canvas);
    canvas.width = window.innerWidth;
    canvas.height = window.innerHeight;
    var gl = canvas.getContext("webgl");

    var vertexShader = gl.createShader(gl.VERTEX_SHADER);
    gl.shaderSource(vertexShader, document.getElementById("x-vertex").text);
    gl.compileShader(vertexShader);
    var fragmentShader = gl.createShader(gl.FRAGMENT_SHADER);
    gl.shaderSource(fragmentShader, document.getElementById("x-fragment").text);
    gl.compileShader(fragmentShader);
    var shaderProgram = gl.createProgram();
    gl.attachShader(shaderProgram, vertexShader);
    gl.attachShader(shaderProgram, fragmentShader);
    gl.linkProgram(shaderProgram);
    gl.useProgram(shaderProgram);

    gl.bindBuffer(gl.ARRAY_BUFFER, gl.createBuffer());
    gl.vertexAttribPointer(gl.getAttribLocation(shaderProgram, "a_label"), 1, gl.FLOAT, false, 0, 0);
    gl.enableVertexAttribArray(gl.getAttribLocation(shaderProgram, "a_label"));
    gl.bufferData(gl.ARRAY_BUFFER, new Float32Array([1,2,3,4]), gl.STATIC_DRAW);
    gl.uniform1f(gl.getUniformLocation(shaderProgram, "u_ratio"), (canvas.width / canvas.height));

    var t = 0;
    function drawScene() {
        gl.uniform1f(gl.getUniformLocation(shaderProgram, "u_time"), t++);
        gl.drawArrays(gl.TRIANGLE_STRIP, 0, 4);
        window.requestAnimationFrame(drawScene);
    }
    drawScene();
</script>