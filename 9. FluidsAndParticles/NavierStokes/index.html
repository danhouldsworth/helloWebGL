<!DOCTYPE html>
<html lang="en">
<head>
    <script id="2d-vertex-shader" type="x-shader/x-vertex">
        attribute vec2 a_position;
        void main() {
           gl_Position = vec4(a_position, 0, 1);
        }
    </script>

    <script id="boundaryConditionsShader" type="x-shader/x-fragment">
        precision highp float;

        uniform sampler2D   u_texture;
        uniform float       u_scale;
        uniform vec2        u_textureSize;

        uniform vec2        u_obstaclePosition;             // scaled to texture size
        uniform float       u_obstacleRad;                  // scaled to texture size
        uniform float       u_obstacleShape;

        uniform float       u_angleAttack;


        void main() {
            vec2    fragCoord       = gl_FragCoord.xy;      // Screen coords
            float   u_obstacleRd1   = u_obstacleRad + 1.0;  // 1 pixel

            vec2 dir = fragCoord - u_obstaclePosition;
            mat2 rotation = mat2(
                +cos(u_angleAttack),  -sin(u_angleAttack),
                +sin(u_angleAttack),  +cos(u_angleAttack)
            );
            dir = dir*rotation;
            float dist = length(dir);
            if (u_obstacleShape == 2.0 ){
            // Circle
                if      (dist < u_obstacleRad)      {gl_FragColor = vec4(0);}
                else if (dist < u_obstacleRd1)      {gl_FragColor = texture2D(u_texture, (fragCoord + dir / dist * 2.0) / u_textureSize) * u_scale;}
                else                                {gl_FragColor = texture2D(u_texture, fragCoord                      / u_textureSize);}
            }
            else if (u_obstacleShape == 1.0 ){
            // Square
                if      (dir.x > -u_obstacleRad && dir.y > -u_obstacleRad && dir.x < u_obstacleRad && dir.y < u_obstacleRad) {gl_FragColor = vec4(0);}
                else if (dir.x > -u_obstacleRd1 && dir.y > -u_obstacleRd1 && dir.x < u_obstacleRd1 && dir.y < u_obstacleRd1) {gl_FragColor = texture2D(u_texture, (fragCoord + dir / dist * 2.0) / u_textureSize) * u_scale;}
                else                                                                                                         {gl_FragColor = texture2D(u_texture, fragCoord                      / u_textureSize);}
            }
            else if (u_obstacleShape == 3.0){
            // Airfoil
                dir /= 2.0 * u_obstacleRad; // Need to scale such chord == 1.0

                if (dir.x >= 0.0 && abs(dir.y) <= (0.25 * (1.4845*sqrt(dir.x) - 0.63*dir.x - 1.758*dir.x*dir.x + 1.4215*dir.x*dir.x*dir.x - 0.5075*dir.x*dir.x*dir.x*dir.x) )){gl_FragColor = vec4(0);}
                // Do same but with a slightly larget (1x Px) obsRadius for boundary?
                else                                                                                                                                                          {gl_FragColor = texture2D(u_texture, fragCoord                      / u_textureSize);}
            }
        }
    </script>

    <script id="2d-render-shader" type="x-shader/x-fragment">
        precision highp float;

        uniform sampler2D   u_material;
        uniform vec2        u_textureSize;
        uniform vec2        u_obstaclePosition;
        uniform float       u_obstacleRad;
        uniform float       u_obstacleShape;

        uniform float       u_colourScaler;

        uniform float       u_angleAttack;

        void main() {
            vec2    fragCoord       = gl_FragCoord.xy;
            vec2    dir             = fragCoord - u_obstaclePosition;
            float   u_obstacleRd1   = u_obstacleRad + 2.0;

            float   dist            = length(dir);
            mat2 rotation = mat2(
                +cos(u_angleAttack),  -sin(u_angleAttack),
                +sin(u_angleAttack),  +cos(u_angleAttack)
            );
            dir = dir*rotation;
            if (u_obstacleShape == 2.0 ){
            // Circle
                if (dist < u_obstacleRd1){
                    gl_FragColor = vec4(1.0, 0, 0.0, 1);
                    return;
                }
            }
            else if (u_obstacleShape == 1.0 ){
            // Square
                if (dir.x > -u_obstacleRd1 && dir.y > -u_obstacleRd1 && dir.x < u_obstacleRd1 && dir.y < u_obstacleRd1){
                    gl_FragColor = vec4(1.0, 0, 0.0, 1);
                    return;
                }
            }
            else if (u_obstacleShape == 3.0 ){
            // Airfoil 0 AoA
                dir /= 2.0 * u_obstacleRad; // Need to scale such chord == 1.0

                if (dir.x >= 0.0 && abs(dir.y) <= (0.25 * (1.4845*sqrt(dir.x) - 0.63*dir.x - 1.758*dir.x*dir.x + 1.4215*dir.x*dir.x*dir.x - 0.5075*dir.x*dir.x*dir.x*dir.x) )){
                    gl_FragColor = vec4(1.0, 0, 0.0, 1);
                    return;
                }

            }

            float mat1 = texture2D(u_material, fragCoord / u_textureSize).x;
            vec3 color = vec3(1.0,1.0,1.0); // vec3(0.98, 0.93, 0.84);
            gl_FragColor = vec4(mat1 * color * u_colourScaler, 1);
        }
    </script>

    <script id="gradientSubtractionShader" type="x-shader/x-fragment">
        precision highp float;

        uniform sampler2D   u_velocity;
        uniform sampler2D   u_pressure;

        uniform vec2        u_textureSize;

        uniform float       u_const;

        vec2 repeatBoundary(vec2 coord){
            coord -= vec2(0.5, 0.5);
            //if (coord.x < 0.0) coord.x = u_textureSize.x-1.0;
            if (coord.x >= u_textureSize.x - 1.0)       {coord.x = 0.0;}
            if      (coord.y < 0.0)                     {coord.y = u_textureSize.y - 1.0;}
            else if (coord.y >= u_textureSize.y - 1.0)  {coord.y = 0.0;}
            coord += vec2(0.5, 0.5);
            return coord;
        }

        void main() {

            vec2 fragCoord = gl_FragCoord.xy;

            vec2 currentVelocity = texture2D(u_velocity, fragCoord / u_textureSize).xy;                     // Velocity Vector2 field .xy

            float n = texture2D(u_pressure, repeatBoundary(fragCoord + vec2(0.0, +1.0)) / u_textureSize).x; // Pressure Scaler field .x
            float s = texture2D(u_pressure, repeatBoundary(fragCoord + vec2(0.0, -1.0)) / u_textureSize).x; // Pressure Scaler field .x
            float e = texture2D(u_pressure, (fragCoord + vec2(+1.0, 0.0)) / u_textureSize).x;               // Pressure Scaler field .x
            float w = texture2D(u_pressure, (fragCoord + vec2(-1.0, 0.0)) / u_textureSize).x;               // Pressure Scaler field .x

            gl_FragColor = vec4(currentVelocity - u_const * vec2(e - w, n - s), 0.0, 0.0);
        }
    </script>

    <script id="divergenceShader" type="x-shader/x-fragment">
        precision highp float;

        uniform sampler2D   u_velocity;
        uniform vec2        u_textureSize;
        uniform float       u_const;

        vec2 repeatBoundary(vec2 coord){
            coord -= vec2(0.5, 0.5);
            if      (coord.x < 0.0)                     {coord.x = u_textureSize.x - 1.0;}
            else if (coord.x >= u_textureSize.x - 1.0)  {coord.x = 0.0;}
            if      (coord.y < 0.0)                     {coord.y = u_textureSize.y - 1.0;}
            else if (coord.y >= u_textureSize.y - 1.0)  {coord.y = 0.0;}
            coord += vec2(0.5, 0.5);
            return coord;
        }

        void main() {

            vec2 fragCoord = gl_FragCoord.xy;

            //finite difference formulation of divergence

            //periodic boundary
            float n = texture2D(u_velocity, repeatBoundary(fragCoord + vec2(0.0, +1.0)) / u_textureSize).y; // Velocity Vector2 field. Vertical held in .y
            float s = texture2D(u_velocity, repeatBoundary(fragCoord + vec2(0.0, -1.0)) / u_textureSize).y;
            float e = texture2D(u_velocity, repeatBoundary(fragCoord + vec2(1.0, +0.0)) / u_textureSize).x; // Velocity Vector2 field. Horizontal held in .x
            float w = texture2D(u_velocity,               (fragCoord + vec2(-1.0, 0.0)) / u_textureSize).x;

            float div = u_const * (e - w + n - s);
            gl_FragColor = vec4(div, 0, 0, 0);
        }
    </script>

    <script id="forceShader" type="x-shader/x-fragment">
        precision highp float;

        uniform sampler2D   u_velocity;

        uniform vec2        u_textureSize;

        uniform vec2        u_mouseCoord;
        uniform vec2        u_mouseDir;

        uniform float       u_reciprocalRadius;

        uniform float       u_dt;

        void main() {

            vec2 fragCoord          = gl_FragCoord.xy;
            vec2 currentVelocity    = texture2D(u_velocity, fragCoord / u_textureSize).xy;
            vec2 pxDist             = fragCoord - u_mouseCoord;
            currentVelocity += u_mouseDir * u_dt * exp(-(pxDist.x * pxDist.x + pxDist.y * pxDist.y) * u_reciprocalRadius);

            gl_FragColor = vec4(currentVelocity, 0, 0);
        }
    </script>

    <script id="jacobiShader" type="x-shader/x-fragment">
        precision highp float;

        uniform sampler2D   u_b;
        uniform sampler2D   u_x;

        uniform vec2        u_textureSize;

        uniform float       u_alpha;
        uniform float       u_reciprocalBeta;

        void main() {

            vec2 fragCoord = gl_FragCoord.xy;

            vec2 currentState = texture2D(u_b, fragCoord / u_textureSize).xy;

            //implicitly solve diffusion via jacobi iteration
            // Velocity vector2 field in / out
            vec2 n = texture2D(u_x, (fragCoord + vec2( 0.0,  1.0)) / u_textureSize).xy;
            vec2 s = texture2D(u_x, (fragCoord + vec2( 0.0, -1.0)) / u_textureSize).xy;
            vec2 e = texture2D(u_x, (fragCoord + vec2( 1.0,  0.0)) / u_textureSize).xy;
            vec2 w = texture2D(u_x, (fragCoord + vec2(-1.0,  0.0)) / u_textureSize).xy;

            vec2 nextState = (n + s + e + w + u_alpha * currentState) * u_reciprocalBeta;

            gl_FragColor = vec4(nextState, 0, 0);
        }
   </script>

    <script id="advectShaderMat" type="x-shader/x-fragment">
        precision highp float;

        uniform sampler2D   u_velocity;
        uniform sampler2D   u_material;

        uniform vec2        u_textureSize;
        uniform float       u_scale;

        uniform float       u_dt;

        vec2 bilinearInterp(vec2 pos, sampler2D texture, vec2 size){
            //bilinear interp between nearest cells

            vec2 pxCenter   = vec2(0.5, 0.5);

            vec2 ceiled     = ceil(pos);
            vec2 floored    = floor(pos);

            vec2 n = texture2D(texture, (ceiled + pxCenter) / size).xy;//actually ne
            vec2 s = texture2D(texture, (floored + pxCenter) / size).xy;//actually sw
            if (ceiled.x != floored.x){
                vec2 se = texture2D(texture, (vec2(ceiled.x, floored.y) + pxCenter) / size).xy;
                vec2 nw = texture2D(texture, (vec2(floored.x, ceiled.y) + pxCenter) / size).xy;
                n = n * (pos.x - floored.x) + nw * (ceiled.x - pos.x);
                s = se * (pos.x - floored.x) + s * (ceiled.x - pos.x);
            }
            vec2 materialVal = n;
            if (ceiled.y != floored.y){
                materialVal = n * (pos.y - floored.y) + s * (ceiled.y - pos.y);
            }
            return materialVal;
        }

        void main() {

            vec2 fragCoord = gl_FragCoord.xy;

            vec2 pxCenter = vec2(0.5, 0.5);

            //bilinear interp
            //vec2 currentVelocity = 1.0/u_scale*texture2D(u_velocity, fragCoord/u_textureSize).xy;
            vec2 currentVelocity = 1.0 / u_scale * bilinearInterp((fragCoord - pxCenter) * u_scale + pxCenter, u_velocity, u_textureSize * u_scale);

            //implicitly solve advection

            if (fragCoord.x < 1.0 || length(currentVelocity) == 0.0) {//boundary or no velocity
                gl_FragColor = vec4(texture2D(u_material, fragCoord / u_textureSize).xy, 0, 0);
                return;
            }

            vec2 pos = fragCoord - pxCenter - u_dt * currentVelocity;

            if (pos.x >= u_textureSize.x - 1.0) {
                gl_FragColor = vec4(0, 0, 0, 0);
                return;
            }

            //periodic boundary in y
            if      (pos.y < 0.0)                    {pos.y += u_textureSize.y - 1.0;}
            else if (pos.y >= u_textureSize.y - 1.0) {pos.y -= u_textureSize.y - 1.0;}
            //OR NON periodic boundary in y
            // if      (pos.y < 0.0)                    {pos.y = 0.0;}
            // else if (pos.y >= u_textureSize.y - 1.0) {pos.y = u_textureSize.y - 1.0;}

            gl_FragColor = vec4(bilinearInterp(pos, u_material, u_textureSize), 0, 0);
        }
    </script>

    <script id="advectShaderVel" type="x-shader/x-fragment">
        precision highp float;

        uniform sampler2D   u_velocity;
        uniform sampler2D   u_material;

        uniform vec2        u_textureSize;
        uniform float       u_scale;

        uniform float       u_dt;

        void main() {

            vec2 fragCoord = gl_FragCoord.xy;

            vec2 currentVelocity = u_scale * texture2D(u_velocity, fragCoord / u_textureSize).xy;

            //implicitly solve advection

            if (length(currentVelocity) == 0.0) {//boundary or no velocity
                gl_FragColor = vec4(texture2D(u_material, fragCoord / u_textureSize).xy, 0, 0);
                return;
            }

            vec2 pxCenter = vec2(0.5, 0.5);
            vec2 pos = fragCoord - pxCenter - u_dt * currentVelocity;

             if (pos.x < 1.0) {
               gl_FragColor = vec4(1.0, 0, 0, 0);
               return;
            }
            if (pos.x >= u_textureSize.x - 1.0) {
                gl_FragColor = vec4(0, 0, 0, 0);
                return;
            }
            if (pos.x >= u_textureSize.x - 1.0) pos.x -= u_textureSize.x - 1.0;

            //periodic boundary in y
            if      (pos.y < 0.0)                   {pos.y += u_textureSize.y - 1.0;}
            else if (pos.y >= u_textureSize.y - 1.0){pos.y -= u_textureSize.y - 1.0;}
            // OR NON periodic boundary in y
            // if      (pos.y < 0.0)                   {pos.y = 0.0;}
            // else if (pos.y >= u_textureSize.y - 1.0){pos.y = u_textureSize.y - 1.0;}

            //bilinear interp between nearest cells
            vec2 ceiled     = ceil(pos);
            vec2 floored    = floor(pos);

            vec2 n = texture2D(u_material, (ceiled + pxCenter) / u_textureSize).xy;//actually ne
            vec2 s = texture2D(u_material, (floored + pxCenter) / u_textureSize).xy;//actually sw
            if (ceiled.x != floored.x){
                vec2 se = texture2D(u_material, (vec2(ceiled.x, floored.y) + pxCenter) / u_textureSize).xy;
                vec2 nw = texture2D(u_material, (vec2(floored.x, ceiled.y) + pxCenter) / u_textureSize).xy;
                n = n * (pos.x - floored.x) + nw * (ceiled.x - pos.x);
                s = se * (pos.x - floored.x) + s * (ceiled.x - pos.x);
            }
            vec2 materialVal = n;
            if (ceiled.y != floored.y){
                materialVal = n * (pos.y - floored.y) + s * (ceiled.y - pos.y);
            }

            gl_FragColor = vec4(materialVal, 0, 0);
        }
    </script>

    <script type="text/javascript" src="GPUMath.js"></script>
    <script type="text/javascript" src="main.js"></script>
</head>
<body>
    <canvas id="glcanvas"></canvas>
    <div>
        <input type="radio" name="visualisation" value="material" checked="checked">material
        <input type="radio" name="visualisation" value="velocity">velocity
        <input type="radio" name="visualisation" value="pressure">pressure<input type="range" id="scalerP" value="5.0" min="1.0" max="10.0">
        <input type="radio" name="visualisation" value="divergence">divergence<input type="range" id="scalerD" value="100.0" min="1.0" max="10000.0">
        <br/>
        <input type="radio" name="obstacleShape" value="square" checked="checked">square
        <input type="radio" name="obstacleShape" value="circle">circle
        <input type="radio" name="obstacleShape" value="airfoil">aifoil(AoA=<input type="range" id="angleAttack" value="0.0" min="-180.0" max="180.0">)
        <br/>
        <button onclick="paused = !paused;">PAUSE!</button>
    </div>
    <text id="display">TBC</text>
</body>
</html>